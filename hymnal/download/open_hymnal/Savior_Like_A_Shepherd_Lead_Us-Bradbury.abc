%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a free, 
% public domain, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont
% (bdumont at ameritech dot net).  I have gone through serious efforts 
% to make sure that no copyrighted material makes it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software, the authors of "noteedit"
% where the initial layouts are done, and the maintainers of the 
% "CyberHymnal" on the web from where most of the lyrics come.
% Undoubtedly, I am also indebted to all of the great Christians who 
% wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2006 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.60000cm
%%exprabove	false
%%measurebox	false
%%footer "Jn 10:3; Ps 23; Jn 10:1-16; Eph 2:4-5; Rom 5:8		"
%

X: 1
T: Savior, Like A Shepherd Lead Us
C: Words: attr. Dorothy A. Thrupp, 1836. 
C: Music: 'Bradbury' William B. Bradbury, 1859.  Setting: The Methodist Hymnal, 1909, alt.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2007 Revision.
S: Music source: The Methodist Hymnal, 1909 Hymn 677.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
V: S1V2 
V: S2V1 clef=bass 
V: S2V2 
K: Eb % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=100] G G G/F/ G/A/ | (B e) (B G) | F G A/c/ B/A/ | G3 z |
w: 1.~Sa- vior, like a shep- herd lead * us, * much we need Thy ten- der care; 
w: 2.~We are Thine, Thou dost be- friend * us, * be the guard- ian of our way; 
w: 3.~Thou hast pro- mised to re- ceive * us, * poor and sin- ful though we be; 
w: 4.~Ear- ly let us seek Thy fa- * vor, * ear- ly let us do Thy will; 
[V: S1V2]  E E E/D/ E/F/ | G2 (G E) | D E F/A/ G/F/ | E3 x |
[V: S2V1]  B, B, B,/B,/ B,/B,/ | B,2 (E B,) | B, B, B,/B,/ B,/B,/ | B,3 z |
[V: S2V2]  E, E, E,/A,/ G,/F,/ | E,2 E,2 | B,, B,, B,,/B,,/ B,,/B,,/ | E,3 x |
% 5
[V: S1V1]  G G G/F/ G/A/ | (B e) (B G) | F G A/c/ B/A/ | G3
w: In Thy plea- sant pas- tures feed * us, * for our use Thy folds pre- pare. 
w: Keep Thy flock, from sin de- fend * us, * seek us when we go a- stray. 
w: Thou hast mer- cy to re- lieve * us, * grace to cleanse and pow'r to free. 
w: Bless- èd Lord and on- ly Sa- * vior, * with Thy love our bo- soms fill. 
[V: S1V2]  E E E/D/ E/F/ | G2 (G E) | D E F/A/ G/F/ | E3
[V: S2V1]  B, B, B,/B,/ B,/B,/ | B,2 (E B,) | B, B, B,/B,/ B,/B,/ | B,3
[V: S2V2]  E, E, E,/A,/ G,/F,/ | E,2 E,2 | B,, B,, B,,/B,,/ B,,/B,,/ | E,3
% 9
[V: S1V1]  B/B/ | c e2 d/c/ | B G2 G/G/ | F c B A | G3
w: Bless- èd Je- sus, bless- èd Je- sus! Thou hast bought us, Thine we are. 
w: Bless- èd Je- sus, bless- èd Je- sus! Hear, O hear us when we pray. 
w: Bless- èd Je- sus, bless- èd Je- sus! We will ear- ly turn to Thee. 
w: Bless- èd Je- sus, bless- èd Je- sus! Thou hast loved us, love us still. 
[V: S1V2]  G/G/ | A c2 B/A/ | G E2 E/E/ | D A G F | E3
[V: S2V1]  E/E/ | E E2 E/E/ | E B,2 B,/B,/ | B, B, B, B, | B,3
[V: S2V2]  E,/E,/ | A, A,2 A,/A,/ | E, E,2 E,/E,/ | B,, B,, B,, B,, | E,3
% 13
[V: S1V1]  B/B/ | c e2 d/c/ | B !fermata!e2 E/F/ | G B A3/2 F/ | E3 |]
w: Bless- èd Je- sus, bless- èd Je- sus! Thou hast bought us, Thine we are. 
w: Bless- èd Je- sus, bless- èd Je- sus! Hear, O hear us when we pray. 
w: Bless- èd Je- sus, bless- èd Je- sus! We will ear- ly turn to Thee. 
w: Bless- èd Je- sus, bless- èd Je- sus! Thou hast loved us, love us still. 
[V: S1V2]  G/G/ | A c2 B/A/ | G G2 E/E/ | E G F3/2 D/ | E3 |]
[V: S2V1]  E/E/ | E E2 E/E/ | E B,2 G,/A,/ | B, B, B,3/2 A,/ | G,3 |]
[V: S2V2]  E,/E,/ | A, A,2 A,/A,/ | E, !invertedfermata!E,2 E,/E,/ | E, E, B,,3/2 B,,/ | E,3 |]
% 18
